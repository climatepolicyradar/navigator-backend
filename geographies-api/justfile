# dev
# should be used for first run
bootstrap:
    @echo "Nothing to bootstrap"

dev:
    docker compose up

# build
# TODO: this needs to be tied into a lifecycle somewhere to make sure it is run if something is added
# sed is used here to remove a whole bunch of gumpf you can not remove with poetry v1 
requirements:
    poetry export --without-hashes --without-urls --format requirements.txt | sed 's/ ;.*//'  > requirements.txt

# Local docker development
ecr-login:
    aws ecr get-login-password --region eu-west-1 | docker login --username AWS --password-stdin $(aws sts get-caller-identity --query 'Account' --output text).dkr.ecr.eu-west-1.amazonaws.com

docker-build tag:
    # @related: GITHUB_SHA_ENV_VAR
    docker buildx build --platform linux/amd64 --output type=docker --build-arg GITHUB_SHA=$(git rev-parse HEAD) -t geographies-api:{{tag}} .

docker-push tag:
    docker tag geographies-api:{{tag}} $(aws sts get-caller-identity --query 'Account' --output text).dkr.ecr.eu-west-1.amazonaws.com/geographies-api:{{tag}}
    docker push $(aws sts get-caller-identity --query 'Account' --output text).dkr.ecr.eu-west-1.amazonaws.com/geographies-api:{{tag}}

deploy-local tag:
    just ecr-login
    just requirements
    just deploy {{tag}}

# environment is not used here, but is required for the github deploy action
deploy tag _:
    just docker-build {{tag}}
    just docker-push {{tag}}
