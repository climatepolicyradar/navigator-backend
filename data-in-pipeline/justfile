dev:
    uv run python -m app.dev

test:
    uv run pytest -vv

test-ci:
    #!/usr/bin/env bash
    # Build and test against the Docker image
    set -euxo pipefail
    docker build -t data-in-pipeline:test -f Dockerfile.test ..
    docker run --rm data-in-pipeline:test uv run pytest -vv

deploy-to-prefect-from-local:
    #!/usr/bin/env bash
    # we use a bash script here to have inline env variables
    # @see: https://just.systems/man/en/setting-variables-in-a-recipe.html
    set -euxo pipefail
    AWS_ACCOUNT_ID=$(aws sts get-caller-identity --query 'Account' --output text)
    DOCKER_REGISTRY="${AWS_ACCOUNT_ID}.dkr.ecr.eu-west-1.amazonaws.com"
    uv run prefect cloud login
    DOCKER_REGISTRY=${DOCKER_REGISTRY} just deploy-to-prefect

deploy-to-prefect:
    uv run python -m app.deploy

# DuckDB to serve transformation results
# This is currently a spike to be able to better analyse, validate and share with others how the transformations are working
duckdb-build:
    uv run python -m app.scripts.build_duckdb

duckdb-dev:
    uv run fastapi dev ./app/scripts/serve_duckdb.py