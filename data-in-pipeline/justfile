dev: 
    uv run python -m app.dev

test-local: 
    uv run pytest -vv

deploy-to-prefect-from-local:
    #!/usr/bin/env bash
    # we use a bash script here to have inline env variables
    # @see: https://just.systems/man/en/setting-variables-in-a-recipe.html
    set -euxo pipefail
    AWS_ACCOUNT_ID=$(aws sts get-caller-identity --query 'Account' --output text)
    DOCKER_REGISTRY="${AWS_ACCOUNT_ID}.dkr.ecr.eu-west-1.amazonaws.com"
    uv run prefect cloud login
    DOCKER_REGISTRY=${DOCKER_REGISTRY} just deploy-to-prefect

build service environment tag:
    #!/usr/bin/env bash
    # Build the Docker image for data-in-pipeline
    set -euxo pipefail
    GITHUB_SHA=$(git rev-parse HEAD)
    GITHUB_REMOTE_URL=$(git config --get remote.origin.url)
    docker buildx build \
        --platform linux/amd64 \
        --output type=docker \
        --build-arg SERVICE={{service}} \
        --build-arg ENV={{environment}} \
        --build-arg GITHUB_SHA=${GITHUB_SHA} \
        --label org.opencontainers.image.source=${GITHUB_REMOTE_URL} \
        --label org.opencontainers.image.revision=${GITHUB_SHA} \
        --label org.opencontainers.image.created=$(date -u +%Y-%m-%dT%H:%M:%SZ) \
        --tag {{service}}:{{tag}} \
        --tag {{service}}:${GITHUB_SHA} \
        --file Dockerfile ..

deploy-override service environment tag: 
    #!/usr/bin/env bash
    # this is run from the root justfile in CI
    # TODO: deploy to staging and production
    # https://linear.app/climate-policy-radar/issue/APP-1549/create-data-in-pipeline-deployments-for-production-and-staging
    set -euxo pipefail
    GITHUB_SHA=$(git rev-parse HEAD)
    AWS_ACCOUNT_ID=$(aws sts get-caller-identity --query 'Account' --output text)
    DOCKER_REGISTRY="${AWS_ACCOUNT_ID}.dkr.ecr.eu-west-1.amazonaws.com"
    
    # Build the image
    just build {{service}} {{environment}} {{tag}}
    
    docker tag {{service}}:{{tag}} ${DOCKER_REGISTRY}/{{service}}:{{tag}}
    docker push ${DOCKER_REGISTRY}/{{service}}:{{tag}}
    
    # Deploy to Prefect
    DOCKER_REGISTRY=${DOCKER_REGISTRY} just deploy-to-prefect

deploy-to-prefect: 
    uv run python -m app.deploy

# DuckDB to serve transformation results
# This is currently a spike to be able to better analyse, validate and share with others how the transformations are working
duckdb-build: 
    uv run python -m app.scripts.build_duckdb

duckdb-dev: 
    uv run fastapi dev ./app/scripts/serve_duckdb.py

build-load-api: 
    docker build -f infra/Dockerfile.load_api -t load-api --platform linux/amd64 ..
